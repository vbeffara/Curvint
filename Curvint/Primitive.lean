import Curvint.to_mathlib
import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Data.Real.StarOrdered

open Set BigOperators Metric Filter MeasureTheory intervalIntegral

variable {f : â„‚ â†’ â„‚} {zâ‚€ z w : â„‚} {Îµ Î´ t a b : â„} {K U : Set â„‚}

theorem mem_segment (ht : t âˆˆ Icc (0 : â„) 1) : (1 - t) â€¢ zâ‚€ + t â€¢ z âˆˆ segment â„ zâ‚€ z :=
  âŸ¨1 - t, t, by linarith [ht.2], ht.1, by ring, rflâŸ©

theorem continuous_bary : Continuous (Î» t : â„ => (1 - t) â€¢ zâ‚€ + t â€¢ z) := by continuity

theorem differentiable_bary : Differentiable â„‚ (Î» z : â„‚ => (1 - t) â€¢ zâ‚€ + t â€¢ z) :=
  (differentiable_const _).add (differentiable_id.const_smul _)

theorem has_deriv_at_bary : HasDerivAt (Î» t : â„ => (1 - t) â€¢ zâ‚€ + t â€¢ z) (z - zâ‚€) t := by
  have h0 : HasDerivAt (1 - Â·) (-1) t := by
    simpa using (hasDerivAt_const t 1).sub (hasDerivAt_id t)
  have h1 : HasDerivAt (Î» t : â„ => (1 - t) â€¢ zâ‚€) (-zâ‚€) t := by
    simpa using h0.smul_const zâ‚€
  have h2 : HasDerivAt (Â· â€¢ z) z t := by
    simpa using (hasDerivAt_id t).smul_const z
  convert h1.add h2 using 1 ; ring

theorem hasDerivAt_bary' : HasDerivAt (Î» z => (1 - t) â€¢ zâ‚€ + t â€¢ z) t z := by
  simpa using (hasDerivAt_const z ((1 - t) â€¢ zâ‚€)).add ((hasDerivAt_id z).const_smul t)

theorem StarConvex.bary (hU : StarConvex â„ zâ‚€ U) (hz : z âˆˆ U) :
    MapsTo (Î» t : â„ => (1 - t) â€¢ zâ‚€ + t â€¢ z) (Icc 0 1) U :=
  Î» _ ht => hU.segment_subset hz (mem_segment ht)

noncomputable def primitive (f : â„‚ â†’ â„‚) (zâ‚€ z : â„‚) : â„‚ :=
  (z - zâ‚€) * âˆ« t in (0:â„)..1, f ((1 - t) â€¢ zâ‚€ + t â€¢ z)

-- theorem primitive_eq_curvint : primitive f zâ‚€ z = curvint 0 1 f (Î» t, (1 - t) â€¢ zâ‚€ + t â€¢ z) :=
-- by simpa only [curvint, has_deriv_at_bary.deriv]
--   using (interval_integral.integral_const_mul _ _).symm

namespace detail

noncomputable abbrev Ï† (f : â„‚ â†’ â„‚) (zâ‚€ z : â„‚) (t : â„) : â„‚ := f ((1 - t) â€¢ zâ‚€ + t â€¢ z)

noncomputable abbrev Ïˆ (f : â„‚ â†’ â„‚) (zâ‚€ z : â„‚) (t : â„) : â„‚ := t â€¢ deriv f ((1 - t) â€¢ zâ‚€ + t â€¢ z)

end detail

open detail

theorem isCompact_segment {ğ•œ E : Type*} [OrderedRing ğ•œ] [TopologicalSpace ğ•œ] [TopologicalAddGroup ğ•œ]
    [CompactIccSpace ğ•œ] [TopologicalSpace E] [AddCommGroup E] [ContinuousAdd E] [Module ğ•œ E]
    [ContinuousSMul ğ•œ E] {x y : E} :
    IsCompact (segment ğ•œ x y) := by
  simpa only [segment_eq_image] using isCompact_Icc.image (by continuity)

theorem DifferentiableOn.exists_primitive (f_holo : DifferentiableOn â„‚ f U)
    (hU : StarConvex â„ zâ‚€ U) (hU' : IsOpen U) (hz : z âˆˆ U) :
    HasDerivAt (primitive f zâ‚€) (f z) z := by

  let Ï† := Ï† f zâ‚€
  let Ïˆ := Ïˆ f zâ‚€
  let I : Set â„ := Icc 0 1

  have f_cont : ContinuousOn f U := f_holo.continuousOn
  have f_deri : âˆ€ â¦ƒzâ¦„ (_ : z âˆˆ U), HasDerivAt f (_root_.deriv f z) z :=
    Î» z hz => f_holo.hasDerivAt (hU'.mem_nhds hz)
  have f_cder : ContinuousOn (_root_.deriv f) U := (f_holo.analyticOnNhd hU').deriv.continuousOn

  have Ï†_cont {z} (hz : z âˆˆ U) : ContinuousOn (Ï† z) I :=
    f_cont.comp continuous_bary.continuousOn (hU.bary hz)
  have Ï†_diff {t} (ht : t âˆˆ I) : DifferentiableOn â„‚ (Î» w => Ï† w t) U :=
    f_holo.comp differentiable_bary.differentiableOn (Î» z hz => hU.bary hz ht)
  have Ï†_derz {z} (hz : z âˆˆ U) {t} (ht : t âˆˆ I) : HasDerivAt (Î» x => Ï† x t) (Ïˆ z t) z := by
    convert (f_deri (hU.bary hz ht)).comp z hasDerivAt_bary' ; simp [Ïˆ] ; ring
  have Ï†_dert {t} (ht : t âˆˆ I) : HasDerivAt (Ï† z) ((z - zâ‚€) * _root_.deriv f ((1 - t) â€¢ zâ‚€ + t â€¢ z)) t := by
    convert (f_deri (hU.bary hz ht)).comp t has_deriv_at_bary using 1 ; ring
  have Ïˆ_cont : ContinuousOn (Ïˆ z) I :=
    continuousOn_id.smul (f_cder.comp continuous_bary.continuousOn (hU.bary hz))

  have step1 : HasDerivAt (Î» z => âˆ« t in (0:â„)..1, Ï† z t) (âˆ« t in (0:â„)..1, Ïˆ z t) z := by
    obtain âŸ¨Î´, Î´_pos, K_subsâŸ© :=
      isCompact_segment.exists_cthickening_subset_open hU' (hU.segment_subset hz)
    let K := cthickening Î´ (segment â„ zâ‚€ z)
    have hzâ‚€ : zâ‚€ âˆˆ K := self_subset_cthickening _ âŸ¨1, 0, zero_le_one, le_rfl, by ring, by simpâŸ©
    have K_cpct : IsCompact K := isCompact_of_isClosed_isBounded isClosed_cthickening
      isCompact_segment.isBounded.cthickening
    have K_conv : Convex â„ K := (convex_segment _ _).cthickening _
    have K_ball : ball z Î´ âŠ† K := ball_subset_closedBall.trans
      (closedBall_subset_cthickening (right_mem_segment _ _ _) Î´)
    obtain âŸ¨C, hCâŸ© := K_cpct.exists_bound_of_continuousOn (f_cder.mono K_subs)
    have C_nonneg : 0 â‰¤ C := (norm_nonneg _).trans (hC zâ‚€ hzâ‚€)

    have key : âˆ€ t âˆˆ I, LipschitzOnWith (Real.nnabs C) (Ï† Â· t) K := by
      refine Î» t ht => lipschitzOnWith_iff_norm_sub_le.mpr (Î» x hx y hy => ?_)
      refine K_conv.norm_image_sub_le_of_norm_deriv_le (f := (Ï† Â· t)) (Î» w hw => ?_) ?_ hy hx
      Â· exact (Ï†_diff ht).differentiableAt (hU'.mem_nhds (K_subs hw))
      Â· rintro w hw
        rw [(Ï†_derz (K_subs hw) ht).deriv]
        have f_bary := hC _ ((K_conv.starConvex hzâ‚€).bary hw ht)
        have ht' : |t| â‰¤ 1 := by { rw [abs_le] ; constructor <;> linarith [ht.1, ht.2] }
        simpa [Ïˆ, abs_eq_self.2 C_nonneg] using mul_le_mul ht' f_bary (by simp) (by simp)

    apply has_deriv_at_integral_of_continuous_of_lip zero_le_one Î´_pos
    Â· exact eventually_of_mem (hU'.mem_nhds hz) (Î» _ => Ï†_cont)
    Â· exact Î» t ht => Ï†_derz hz (Ioc_subset_Icc_self ht)
    Â· exact Î» t ht => (key t (Ioc_subset_Icc_self ht)).mono K_ball
    Â· exact Ïˆ_cont.mono Ioc_subset_Icc_self

  have step2 : (âˆ« t in (0:â„)..1, Ï† z t) + (z - zâ‚€) * (âˆ« t in (0:â„)..1, Ïˆ z t) = f z := by
    let g (t : â„) : â„‚ := t â€¢ Ï† z t
    let h (t : â„) : â„‚ := Ï† z t + (z - zâ‚€) * Ïˆ z t

    have g_cont : ContinuousOn g I := continuousOn_id.smul (Ï†_cont hz)
    have g_dert : âˆ€ t âˆˆ Ioo (0:â„) 1, HasDerivAt g (h t) t := by
      rintro t ht
      convert (hasDerivAt_id t).smul (Ï†_dert (Ioo_subset_Icc_self ht)) using 1
      simp [Ïˆ, h, add_comm] ; ring
    have h_intg : IntervalIntegrable h volume (0:â„) 1 := by
      apply ContinuousOn.intervalIntegrable
      simp only [h, min_eq_left, zero_le_one, max_eq_right]
      convert (Ï†_cont hz).add (continuousOn_const.mul Ïˆ_cont)
      simp [I]

    convert â† integral_eq_sub_of_hasDerivAt_of_le zero_le_one g_cont g_dert h_intg using 1
    Â· simp only [Ïˆ, h]
      rw [intervalIntegral.integral_add]
      Â· simp
      Â· apply ContinuousOn.intervalIntegrable
        convert Ï†_cont hz
        simp [I]
      Â· apply ContinuousOn.intervalIntegrable
        refine continuousOn_const.mul ?_
        convert Ïˆ_cont
        simp [I]
    Â· simp [g, Ï†, detail.Ï†]

  have : HasDerivAt (primitive f zâ‚€)
      ((âˆ« t in (0:â„)..1, Ï† z t) + (z - zâ‚€) * âˆ« t in (0:â„)..1, Ïˆ z t) z := by
    convert ((hasDerivAt_id z).sub (hasDerivAt_const z zâ‚€)).mul step1 using 1
    simp

  exact step2 â–¸ this
